/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}

kotlin {
    ios()
    js(IR) {
        nodejs()
        binaries.executable()
    }
    jvm()

    sourceSets {
        all {
            languageSettings {
                progressiveMode = true
            }
        }
        commonMain {
            dependencies {
                api(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
                implementation("co.touchlab:stately-concurrency:1.2.0")
                implementation("co.touchlab:stately-iso-collections:1.2.0")
            }
        }
        commonTest {
            dependencies {
                implementation(project(":internal-testutils-ktx"))
                implementation(project(":internal-testutils-paging"))
                implementation(libs.kotlinTest)
                implementation(libs.kotlinCoroutinesTest)
                implementation("com.willowtreeapps.assertk:assertk:0.25")
            }
        }
        nonJsMain {
            dependsOn(commonMain)
        }
        nonJsTest {
            dependsOn(commonTest)
        }
        iosMain {
            dependsOn(nonJsMain)
        }
        jsMain {
            dependencies {
                api(libs.kotlinStdlibJs)
            }
        }
        jvmMain {
            dependsOn(nonJsMain)
        }
        jvmTest {
            dependencies {
                implementation(libs.junit)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(project(":internal-testutils-ktx"))
                implementation(project(":internal-testutils-paging"))
                implementation(libs.kotlinTestJunit)
            }
        }
    }
}

androidx {
    name = "Android Paging-Common"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.PAGING
    inceptionYear = "2017"
    description = "Android Paging-Common"
}
