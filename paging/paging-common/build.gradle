/*
 * Copyright (C) 2017 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}

kotlin {
    js(IR) {
        nodejs()
        binaries.executable()
    }

    jvm()

    ios()
    iosSimulatorArm64()
    linuxArm64()
    linuxX64()
    macosArm64()
    macosX64()
    mingwX64()
    tvos()
    tvosSimulatorArm64()
    watchos()
    watchosSimulatorArm64()

    sourceSets {
        all {
            languageSettings {
                progressiveMode = true
            }
        }
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
                implementation("co.touchlab:stately-concurrency:1.2.0")
                implementation("co.touchlab:stately-iso-collections:1.2.0")
            }
        }
        commonTest {
            dependencies {
                implementation(project(":internal-testutils-paging"))
                implementation(libs.kotlinTest)
                implementation(libs.kotlinCoroutinesTest)
                implementation(project(":internal-testutils-kmp"))
                implementation("com.willowtreeapps.assertk:assertk:0.25")
            }
        }
        nonJsMain {
            dependsOn(commonMain)
        }
        nonJsTest {
            dependsOn(commonTest)
        }
        jvmTest {
            dependencies {
                implementation(libs.junit)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(project(":internal-testutils-paging"))
                implementation(libs.kotlinTestJunit)
            }
        }

        targets.each { target ->
            if (target.platformType == KotlinPlatformType.common) return
            if (target.platformType != KotlinPlatformType.js) {
                target.compilations.main.defaultSourceSet.dependsOn(sourceSets.nonJsMain)
                target.compilations.test.defaultSourceSet.dependsOn(sourceSets.nonJsTest)
            }
        }
    }
}

androidx {
    name = "Android Paging-Common"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2017"
    description = "Android Paging-Common"
}
