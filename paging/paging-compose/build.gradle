/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.PlatformIdentifier
import androidx.build.Publish
import androidx.build.RunApiTasks
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("org.jetbrains.compose") version "1.5.12"
}

androidXMultiplatform {
    android()
    js {
        nodejs()
        binaries.executable()
    }
    jvm()
    ios()
    linuxX64() // TODO Replace with linux() once org.jetbrains.compose.runtime:runtime:1.5.12 has a linxArm64() target (https://github.com/JetBrains/compose-multiplatform/issues/3344).
    mac()
    mingw()
    tvos()
    watchos()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api(project(":paging:paging-common"))
                api("org.jetbrains.compose.runtime:runtime:1.5.12")
            }
        }

        androidMain {
            dependencies {
                api("androidx.compose.foundation:foundation:1.5.0-beta03")
            }
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        nonAndroidTest {
            dependsOn(commonTest)
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        targets.each { target ->
            if (target.platformType == KotlinPlatformType.common) return
            if (target.platformType != KotlinPlatformType.androidJvm) {
                target.compilations.main.defaultSourceSet.dependsOn(kotlinExtension.sourceSets.nonAndroidMain)
                target.compilations.test.defaultSourceSet.dependsOn(kotlinExtension.sourceSets.nonAndroidTest)
            }
        }
    }
}

androidx {
    name = "Paging-Compose"
    publish = Publish.SNAPSHOT_AND_RELEASE
    inceptionYear = "2020"
    description = "Compose integration with Paging"
    runApiTasks = new RunApiTasks.Yes()
    legacyDisableKotlinStrictApiMode = true
    samples(project(":paging:paging-compose:paging-compose-samples"))
}

android {
    namespace "androidx.paging.compose"
}
