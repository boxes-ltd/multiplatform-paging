/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.RunApiTasks

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
    id("org.jetbrains.kotlin.multiplatform")
    id("org.jetbrains.compose") version "1.4.0"
}

// TODO Remove once there is a compatible version of Jetbrains Compose for Kotlin 1.8.21.
compose {
    kotlinCompilerPlugin.set(dependencies.compiler.forKotlin("1.8.20"))
    kotlinCompilerPluginArgs.add("suppressKotlinVersionCompatibilityCheck=1.8.21")
}

kotlin {
    js(IR) {
        nodejs()
        binaries.executable()
    }

    jvm()

    ios()
    iosSimulatorArm64()
    linuxX64()
    macosArm64()
    macosX64()
    mingwX64()
    tvos()
    tvosSimulatorArm64()
    watchos()
    watchosSimulatorArm64()

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api(project(":paging:paging-common"))
                api(compose.runtime)
            }
        }
        jvmTest {
            dependencies {
                implementation(projectOrArtifact(":internal-testutils-paging"))
                implementation(project(":compose:test-utils"))
                implementation(libs.junit)
                implementation(libs.kotlinCoroutinesTest)
                implementation(libs.testRunner)
                implementation(libs.truth)
                implementation(compose.desktop.currentOs)
                implementation(compose.uiTestJUnit4)
            }
        }
    }
}

androidx {
    name = "Android Paging-Compose"
    mavenVersion = LibraryVersions.PAGING_COMPOSE
    inceptionYear = "2020"
    description = "Compose integration with Paging"
    runApiTasks = new RunApiTasks.Yes()
    legacyDisableKotlinStrictApiMode = true
}
